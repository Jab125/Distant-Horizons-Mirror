pluginManagement {
    repositories {
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://maven.minecraftforge.net/" }
        maven { url "https://repo.spongepowered.org/repository/maven-public/"}
        gradlePluginPortal()
    }
}

// Minecraft dependent sub-projects
include("common")
include("fabric")

// The logic for buildForge can be made more succinct, but the readability goes way down.
def buildForge = true
if (gradle.startParameter.taskRequests.size() > 0) {
    if (gradle.startParameter.taskRequests[0].args.size() > 0) {
        if (gradle.startParameter.taskRequests[0].args[0].startsWith("fabric:")) {
            buildForge = false
        }
    }
}

if (buildForge) {
    include("forge")
}

// Minecraft independent sub-projects
include("core")
project(":core").projectDir = file('coreSubProjects/core')
include("api")
project(":api").projectDir = file('coreSubProjects/api')

rootProject.name = "DistantHorizons"
